grammar io.github.jhipster.jdl.JDL with org.eclipse.xtext.common.Terminals

generate jdl "http://www.github.io/jhipster/jdl/JDL"

/* =========================================================================
 * ROOT
 * ========================================================================= */
JdlDomainModel:
	features+=JdlFeature*;

JdlFeature:
	JdlEntity | JdlEnum | JdlRelationships | JdlOption;

/* =========================================================================
 * ENTITY
 * ========================================================================= */
JdlEntity:
	'entity' name=ID ('{'
	(fields+=JdlEntityField (',' fields+=JdlEntityField)*)?
	'}')?;

JdlEntityField:
	name=ID type=JdlFieldType;

JdlFieldType:
	JdlEnumField | JdlStringField | JdlNumericField | JdlBooleanField | JdlDateField | JdlBlobField;

JdlEnumField returns JdlEntityField:
	enumType=[JdlEnum] validators+=JdlRequiredValidator*;

JdlStringField returns JdlEntityField:
	stringType?='String' validators+=JdlStringValidators*;

JdlNumericField returns JdlEntityField:
	numericFieldType=JdlNumericFieldType validators+=JdlNumericValidators*;

enum JdlNumericFieldType:
	Integer | Long | BigDecimal | Float | Double;

JdlBooleanField returns JdlEntityField:
	booleanType?='Boolean' validators+=JdlRequiredValidator*;

JdlDateField returns JdlEntityField:
	dateFieldType=JdlDateFieldType validators+=JdlRequiredValidator*;

enum JdlDateFieldType:
	LocalDate | ZonedDateTime;
	
JdlBlobField returns JdlFieldType:
	blobFieldType=JdlBlobFieldType validators+=JdlBlobValidators*;

enum JdlBlobFieldType:
	Blob | AnyBlob | ImageBlob;

/* =========================================================================
 * VALIDATOR
 * ========================================================================= */
JdlStringValidators:
	required?=JdlRequiredValidator |
	minlength=JdlMinLengthValidator |
	maxlength=JdlMaxLengthValidator |
	pattern=JdlPatternValidator;

JdlNumericValidators:
	required?=JdlRequiredValidator | 
	min=JdlMinValidator | 
	max=JdlMaxValidator;

JdlBlobValidators:
	required?=JdlRequiredValidator |
	minbytes=JdlMinBytesValidator | 
	maxbytes=JdlMaxBytesValidator;

JdlRequiredValidator:
	required?='required';

JdlMinLengthValidator:
	'minlength' '(' value=INT ')';

JdlMaxLengthValidator:
	'maxlength' '(' value=INT ')';

JdlPatternValidator:
	'pattern' '(' value=STRING ')';

JdlMinValidator:
	'min' '(' value=INT ')';

JdlMaxValidator:
	'max' '(' value=INT ')';

JdlMinBytesValidator:
	'minbytes' '(' value=INT ')';

JdlMaxBytesValidator:
	'maxbytes' '(' value=INT ')';

/* =========================================================================
 * RELATIONSHIP
 * ========================================================================= */
JdlRelationships:
	'relationship' cardinality=JdlCardinality '{'
	relationships+=JdlRelationship* (',' relationships+=JdlRelationship)*
	'}';

JdlRelationship:
	fromEntity=[JdlEntity] fromName=JdlRelationshipName? 'to' toEntity=[JdlEntity] toName=JdlRelationshipName?;

JdlRelationshipName:
	'{' name=ID ('(' role=ID ')')? '}';

enum JdlCardinality:
	OneToMany | ManyToOne | OneToOne | ManyToMany;

/* =========================================================================
 * ENUM
 * ========================================================================= */
JdlEnum:
	'enum' name=ID '{'
	values+=ID (',' values+=ID)*
	'}';

/* =========================================================================
 * OPTION 
 * ========================================================================= */
JdlOption:
	setting=JdlOptionType (excludes=JdlEntityExclusion)?;

JdlOptionType:
	JdlDtoOption |
	JdlPaginateOption |
	JdlServiceOption |
	JdlMicroserviceOption |
	JdlSearchOption |
	JdlSkipClientOption |
	JdlSkipServerOption |
	JdlAngularSuffixOption |
	JdlNoFluentMethodOption;

JdlWithEntityInclusion:
	(predicate=JdlWildcardPredicate | entities+=[JdlEntity] (',' entities+=[JdlEntity])*) 'with';

JdlForEntityInclusion:
	'for' (predicate=JdlWildcardPredicate | entities+=[JdlEntity] (',' entities+=[JdlEntity])*);

JdlWildcardPredicate:
	wildcard?='*' | all?='all';

JdlEntityExclusion:
	'except' entities+=[JdlEntity] (',' entities+=[JdlEntity])*;

/* =========================================================================
 * DTO OPTION 
 * ========================================================================= */
JdlDtoOption returns JdlOption:
	dtoOption?='dto' includes=JdlWithEntityInclusion dtoType=JdlDtoType;

enum JdlDtoType:
	mapstruct;

/* =========================================================================
 * PAGINATION OPTION 
 * ========================================================================= */
JdlPaginateOption returns JdlOption:
	paginateOption?='paginate' includes=JdlWithEntityInclusion paginateType=JdlPaginateType;

JdlPaginateType:
	pagination?=('pager' | 'pagination') | infiniteScroll?='infinite-scroll';

/* =========================================================================
 * SERVICE OPTION 
 * ========================================================================= */
JdlServiceOption returns JdlOption:
	serviceOption?='service' includes=JdlWithEntityInclusion serviceType=JdlServiceType;

enum JdlServiceType:
	serviceClass | serviceImpl;

/* =========================================================================
 * MICRO SERVICE OPTION 
 * ========================================================================= */
JdlMicroserviceOption returns JdlOption:
	microserviceOption?='microservice' includes=JdlWithEntityInclusion appname=ID;

/* =========================================================================
 * SEARCH OPTION 
 * ========================================================================= */
JdlSearchOption returns JdlOption:
	searchOption?='search' includes=JdlWithEntityInclusion searchType=JdlSearchType;

enum JdlSearchType:
	elasticsearch;

/* =========================================================================
 * SKIP CLIENT OPTION 
 * ========================================================================= */
JdlSkipClientOption returns JdlOption:
	skipClienOption?='skipClient' includes=JdlForEntityInclusion;

/* =========================================================================
 * SKIP SERVER OPTION 
 * ========================================================================= */
JdlSkipServerOption returns JdlOption:
	skipServerOption?='skipServer' includes=JdlForEntityInclusion;

/* =========================================================================
 * ANGULAR SUFFIX OPTION 
 * ========================================================================= */
JdlAngularSuffixOption returns JdlOption:
	angularSuffixOption?='angularSuffix' includes=JdlWithEntityInclusion id=ID;

/* =========================================================================
 * NO FLUENT METHOD OPTION 
 * ========================================================================= */
JdlNoFluentMethodOption returns JdlOption:
	noFluentMethodOption?='noFluentMethod' includes=JdlForEntityInclusion;

